// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
DRegister(in=coumputeOut, load=Dsel, out=outdreg); 
ARegister(in=inputAreg, load=Asel, out=outareg);
Not(in=instruction[15], out=instA);
Not(in=instA, out=instC);    
And(a=instC, b=instruction[5], out=computedA);   
Mux16(a=instruction, b=coumputeOut, sel=computedA, out=inputAreg);    
Or(a=instA, b=computedA, out=Asel);   
Mux16(a=outareg, b=inM, sel=instruction[12], out=AandM);  
And(a=instC, b=instruction[4], out=Dsel);           
ALU(x=outdreg, y=AandM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=coumputeOut, zr=zr1, ng=ng1);        
And(a=zr1, b=instruction[1], out=JUMPeq);
And(a=ng1, b=instruction[2], out=JUMPlt); 
And16(a=true, b=outareg, out[0..14]=addressM);
And(a=instC, b=instruction[3],out=writeM);
And16(a=true, b=coumputeOut, out=outM);
Or(a=zr1, b=ng1, out=zeroOrNeg);
Not(in=zeroOrNeg, out=positive);            
And(a=positive, b=instruction[0], out=JUMPgt);
And(a=instC, b=Ajump, out=pcsel);
Not(in=pcsel, out=pctoC);                 
PC(in=outareg, inc=pctoC, load=pcsel, reset=reset, out[0..14]=pc);
Or(a=JUMPeq, b=JUMPlt, out=jle);
Or(a=jle, b=JUMPgt, out=Ajump);  
    
}